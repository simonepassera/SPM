mpl_top    = 0.4 # inch  outer top margin, title goes here
mpl_bot    = 0.5 # inch  outer bottom margin, x label goes here
mpl_left   = 0.5 # inch  outer left margin, y label goes here
mpl_right  = 0.5 # inch  outer right margin, y2 label goes here
mpl_height = 3 # inch  height of individual plots
mpl_width  = 3 # inch  width of individual plots
mpl_dx     = 1.0 # inch  inter-plot horizontal spacing
mpl_dy     = 0.25 # inch  inter-plot vertical spacing
mpl_ny     = 2   # number of rows
mpl_nx     = 2   # number of columns

# Calculate full dimensions
xsize = mpl_left+mpl_right+(mpl_width*mpl_nx)+(mpl_nx-1)*mpl_dx
ysize = mpl_top+mpl_bot+(mpl_ny*mpl_height)+(mpl_ny-1)*mpl_dy

# Placement functions
# Rows are numbered from bottom to top
bot(n) = (mpl_bot+(n-1)*mpl_height+(n-1)*mpl_dy)/ysize
top(n)  = 1-((mpl_top+(mpl_ny-n)*(mpl_height+mpl_dy))/ysize)

# Columns are numbered from left to right
left(n) = (mpl_left+(n-1)*mpl_width+(n-1)*mpl_dx)/xsize
right(n)  = 1-((mpl_right+(mpl_nx-n)*(mpl_width+mpl_dx))/xsize)

# Initialization
set terminal pdfcairo enhanced size xsize,ysize
set encoding iso_8859_1
set tics scale 1.5
set output 'plot-tocrop.pdf'
set offsets
set autoscale fix
set size ratio 0.9

# set separator for csv file
set datafile separator ';'
set key autotitle columnhead

# Start plotting
set multiplot

# Grid style
set grid back lw 1 lt 0 lc rgb '#808080'

# Key style
set key horizontal font "Helvetica,10" top left Left reverse 
set key vertical maxcolumns 1

# Line styles
LINEWIDTH = 'lw 2.5'
set style line 1 @LINEWIDTH lt 1 lc rgb "#ff6600" pointtype 11 pointsize 0.7
set style line 2 @LINEWIDTH lt 1 lc rgb "#3366ff" pointtype 9 pointsize 0.7
set style line 3 @LINEWIDTH lt 1 lc rgb "#428800" pointtype 7 pointsize 0.7
set style line 4 @LINEWIDTH lt 1 lc rgb "#f368e0" pointtype 1 pointsize 0.7
set style line 5 @LINEWIDTH lt 1 lc rgb "#55E6C1" pointtype 2 pointsize 0.7
set style line 6 @LINEWIDTH lt 1 lc rgb "#b8e994" pointtype 3 pointsize 0.7
set style line 7 @LINEWIDTH lt 1 lc rgb "#d4c820" pointtype 4 pointsize 0.7
set style line 8 @LINEWIDTH lt 1 lc rgb "#ff0000" pointtype 5 pointsize 0.7
set style line 9 @LINEWIDTH lt 1 lc rgb "#D81B60" pointtype 6 pointsize 0.7
set style line 10 @LINEWIDTH lt 1 lc rgb "#33cccc" pointtype 7 pointsize 0.7
set style line 11 @LINEWIDTH lt 1 lc rgb "#fdcb6e" pointtype 7 pointsize 0.7
set style fill transparent solid 0.25 noborder

# Define x-axis settings for all subplots
set mxtics 1

id(x) = x

# Subplot 2-1
# Set horizontal margins for first column
set lmargin at screen left(1)
set rmargin at screen right(1)
# Set horizontal margins for third row (top)
set tmargin at screen top(2)
set bmargin at screen bot(2)
set title 'Number of R-Workers vs Speedup on spmcluster.unipi.it' font "Helvetica,12" offset 0,-0.5,0
set xlabel "Number of R-Workers" font "Helvetica,12" offset 0,0.4,0
set ylabel "Absolute Speedup" font "Helvetica, 12" offset 0.4,0,0
set yrange[0:3]
set xrange[0:32]
#set log y
set ytics 0, 1, 2 font "Helvetica,11" offset 0,0,0 nomirror
set xtics ("1" 1,"2" 2,"4" 4,"8" 8, "16" 16 ,"32" 32) font "Helvetica,11" offset 0,0,0 nomirror
#set arrow from graph 0,0 to 64,64 nohead dt "-"
plot "extra0.csv" using 1:2  with linespoint ls 1 title "Word-Count L-Workers=8 extra=0 num\\\_lines=100",\
     '' using 1:3 with linespoint ls 7 title "Word-Count L-Workers=8 extra=0 num\\\_lines=1000",\
     '' using 1:4 with linespoint ls 3 title "Word-Count L-Workers=8 extra=0 num\\\_lines=10000"
     

# Subplot 1-2
# Set horizontal margins for second column
set lmargin at screen left(2)
set rmargin at screen right(2)
# Set horizontal margins for third row (top)
set tmargin at screen top(2)
set bmargin at screen bot(2)
set title 'Number of R-Workers vs Speedup on spmcluster.unipi.it' font "Helvetica,12" offset 0,-0.5,0
set xlabel "Number of R-Workers" font "Helvetica,12" offset 0,0.4,0
set ylabel "Absolute Speedup" font "Helvetica, 12" offset 0.4,0,0
set yrange[0:3]
set xrange[0:32]
#set log y
set ytics 0, 1, 2 font "Helvetica,11" offset 0,0,0 nomirror
set xtics ("1" 1,"2" 2,"4" 4,"8" 8, "16" 16 ,"32" 32) font "Helvetica,11" offset 0,0,0 nomirror
#set arrow from graph 0,0 to 40,40 nohead dt "-"
plot "extra1000.csv" using 1:2  with linespoint ls 1 title "Word-Count L-Workers=8 extra=1000 num\\\_lines=100",\
     '' using 1:3 with linespoint ls 7 title "Word-Count L-Workers=8 extra=1000 num\\\_lines=1000",\
     '' using 1:4 with linespoint ls 3 title "Word-Count L-Workers=8 extra=1000 num\\\_lines=10000"


# Subplot 2-1
# Set horizontal margins for first column
set lmargin at screen left(1)
set rmargin at screen right(1)
# Set horizontal margins for third row (top)
set tmargin at screen top(1)
set bmargin at screen bot(1)
set title 'Number of R-Workers vs Speedup on spmcluster.unipi.it' font "Helvetica,12" offset 0,-0.5,0
set xlabel "Number of R-Workers" font "Helvetica,12" offset 0,0.4,0
set ylabel "Absolute Speedup" font "Helvetica, 12" offset 0.4,0,0
set yrange[0:12]
set xrange[0:32]
#set logscale y 10
#set log y
set ytics 0, 1, 10 font "Helvetica,11" offset 0,0,0 nomirror
set xtics ("1" 1,"2" 2,"4" 4,"8" 8, "16" 16,"32" 32) font "Helvetica,11" offset 0,0,0 nomirror
#unset arrow
plot "extra10000.csv" using 1:2  with linespoint ls 1 title "Word-Count L-Workers=8 extra=10000 num\\\_lines=100",\
     '' using 1:3 with linespoint ls 7 title "Word-Count L-Workers=8 extra=10000 num\\\_lines=1000",\
     '' using 1:4 with linespoint ls 3 title "Word-Count L-Workers=8 extra=10000 num\\\_lines=10000"

# Subplot 2-2
# Set horizontal margins for second column
set lmargin at screen left(2)
set rmargin at screen right(2)
# Set horizontal margins for third row (top)
set tmargin at screen top(1)
set bmargin at screen bot(1)
set title 'Number of R-Workers vs Speedup on spmcluster.unipi.it' font "Helvetica,12" offset 0,-0.5,0
set xlabel "Number of R-Workers" font "Helvetica,12" offset 0,0.4,0
set ylabel "Absolute Speedup" font "Helvetica, 12" offset 0.4,0,0
set yrange[0:4]
set xrange[0:8]
set ytics 0, 1, 3 font "Helvetica,11" offset 0,0,0 nomirror
set xtics ("1" 1,"2" 2,"4" 4,"8" 8) font "Helvetica,11" offset 0,0,0 nomirror
set key font "Helvetica,9" 
plot "mapping.csv" using 1:2  with linespoint ls 1 title "Word-Count MAPPING=NO     L-Workers=8 extra=0 num\\\_lines=100",\
     '' using 1:3 with linespoint ls 7 title "Word-Count MAPPING=NO     L-Workers=8 extra=0 num\\\_lines=1000",\
     '' using 1:4 with linespoint ls 3 title "Word-Count MAPPING=NO     L-Workers=8 extra=0 num\\\_lines=10000",\
     '' using 1:5 with linespoint ls 2 title "Word-Count MAPPING=CPU0 L-Workers=8 extra=0 num\\\_lines=100",\
     '' using 1:6 with linespoint ls 8 title "Word-Count MAPPING=CPU0 L-Workers=8 extra=0 num\\\_lines=1000",\
     '' using 1:7 with linespoint ls 10 title "Word-Count MAPPING=CPU0 L-Workers=8 extra=0 num\\\_lines=10000"
     
